buildscript {
    ext.kotlin_version = '1.9.22'
    ext.ausweisapp_version = '2.1.1'
    ext.sdkwrapper_version = "$ausweisapp_version.0"

    repositories {
        google()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jmailen.gradle:kotlinter-gradle:4.2.0"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.7.7"
        classpath 'com.github.ben-manes:gradle-versions-plugin:+'
    }
}

plugins {
    id("org.sonarqube") version "4.4.1.3373"
    id("java")
}

version = sdkwrapper_version

def isNonStable = { String version ->
  def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://repo.govkg.de/repository/maven-public"
        }
        maven { url "$project.rootDir/ausweisapp" }
    }
    apply plugin: com.github.benmanes.gradle.versions.VersionsPlugin
    tasks.named("dependencyUpdates").configure {
      checkForGradleUpdate = false
      rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
      }
    }
}

task tarball(type: Tar) {
    into ('/') {
        from rootDir
        include '**'
        exclude ('**/build/**', 'tester', '.reviewboardrc', '.gradle', '.idea', 'jenkins', '**/*.aar')
     }

    destinationDirectory = file('build/tar')
    compression = Compression.GZIP
}
